#define ESD_PIN 4
#define RFID_INPUT 8
#define RELAY_OUTPUT 5

unsigned long validationTimestamp = 0;
unsigned long timeout = 180000;

bool rfidValidated = false;
bool esdDetected = false;

void setup() {
    Serial.begin(9600);
    Serial.println("Waiting for validation signal...");
    pinMode(ESD_PIN, INPUT_PULLUP);
    pinMode(RFID_INPUT, INPUT_PULLUP);
    pinMode(RELAY_OUTPUT, OUTPUT);
    digitalWrite(RELAY_OUTPUT, HIGH);
}

void resetSequence() {
    rfidValidated = false;
    esdDetected = false;
    validationTimestamp = 0;
    Serial.println("Sequence reset.");
}

void triggerRelay() {
    digitalWrite(RELAY_OUTPUT, LOW);
    Serial.println("Gate Opened");
    delay(1000);
    digitalWrite(RELAY_OUTPUT, HIGH);
}

void loop() {

    if (!rfidValidated && digitalRead(RFID_INPUT) == LOW) {
        delay(50);
        if (digitalRead(RFID_INPUT) == LOW) {
            rfidValidated = true;
            validationTimestamp = millis();
            Serial.println("Rfid validation received. Waiting for ESD...");
        }
    }

    if (rfidValidated && !esdDetected) {
        if (digitalRead(ESD_PIN) == LOW) {
            delay(50); // debounce
            if (digitalRead(ESD_PIN) == LOW) {
                esdDetected = true;
                Serial.println("ESD detected after validation. Opening gate...");
                delay(1000);
                triggerRelay();
                resetSequence();
            }
        }

        if (millis() - validationTimestamp > timeout) {
            Serial.println("Timeout: ESD not detected in time. Resetting...");
            resetSequence();
        }
    }
}
